// Builds a Ghidra Extension for a given Ghidra installation.
//
// An absolute path to the Ghidra installation directory must be supplied either by setting the 
// GHIDRA_INSTALL_DIR environment variable or Gradle project property:
//
//     > export GHIDRA_INSTALL_DIR=<Absolute path to Ghidra> 
//     > gradle
//
//         or
//
//     > gradle -PGHIDRA_INSTALL_DIR=<Absolute path to Ghidra>
//
// Gradle should be invoked from the directory of the project to build.  Please see the
// application.gradle.version property in <GHIDRA_INSTALL_DIR>/Ghidra/application.properties
// for the correction version of Gradle to use for the Ghidra installation you specify.

apply plugin: 'java'
apply plugin: 'eclipse'
//----------------------START "DO NOT MODIFY" SECTION------------------------------
def ghidraInstallDir

if (System.env.GHIDRA_INSTALL_DIR) {
	ghidraInstallDir = System.env.GHIDRA_INSTALL_DIR
}
else if (project.hasProperty("GHIDRA_INSTALL_DIR")) {
	ghidraInstallDir = project.getProperty("GHIDRA_INSTALL_DIR")
}

if (ghidraInstallDir) {
	apply from: new File(ghidraInstallDir).getCanonicalPath() + "/support/buildExtension.gradle"
}
else {
	throw new GradleException("GHIDRA_INSTALL_DIR is not defined!")
}
//----------------------END "DO NOT MODIFY" SECTION-------------------------------

// from https://github.com/astrelsky/vscode-ghidra-skeleton/blob/master/skeleton/build.gradle
def docs = file(ghidraInstallDir+'/docs/GhidraAPI_javadoc.zip')

repositories { mavenCentral() }

dependencies {
	testImplementation 'junit:junit:4.13'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	runtimeOnly fileTree(dir: ghidraInstallDir + '/Ghidra', include: "**/*.jar", exclude: project.name + '**')
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

eclipse.classpath.file.whenMerged {
	File javaDoc = new File(ghidraInstallDir+"/docs/GhidraAPI_javadoc.zip");
	for (entry in entries) {
		if (entry.path.contains('jar')) {
			File folder = new File(entry.getPath()).getParentFile();
			for (File file : folder.listFiles()) {
				if (file.getName().endsWith(".zip")) {
					if (file.getName().contains("-src")) {
						entry.setSourcePath(it.fileReference(file));
					}
					entry.setJavadocPath(it.fileReference(javaDoc));
				}
			}
		}
	}
}

buildExtension {
	exclude '.vscode'
	exclude 'gradle*'
	exclude '.editorconfig'
	exclude 'java-formatter.xml'
	exclude '3rdparty'
	exclude 'amiga_ndk39.prf'
	exclude 'test'
}

task install(type: Copy) {
    def outputDir = file("${ghidraInstallDir}/Ghidra/Extensions")
	delete fileTree("${ghidraInstallDir}/Ghidra/Extensions/${project.name}").files
	from zipTree(tasks.buildExtension.archiveFile)
    into outputDir
	def zipFile = fileTree('dist').files[0]
}
